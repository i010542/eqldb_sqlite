// EQL BNF

程序 ::= 语句 | 程序 ',' 语句

语句 ::= 查询语句 | 计算语句 | 答案语句

查询语句 ::= 查询表达式列表 {筛选子句} [分组子句] [排序子句]

查询表达式列表 ::= 查询表达式 | 查询表达式列表 逻辑运算符 查询表达式

查询表达式 ::= 事实表达式 | 比较表达式
逻辑运算符 ::= '\and' | '\or' | '\not'

事实表达式 ::= spo表达式 | spo表达式 qv表达式
spo表达式 ::= s表达式 ':' p表达式 ':' o表达式
s表达式 ::= 变量 | 常量 | '(' 变量 ':' 常量 ')'
p表达式 ::= 变量 | 常量
o表达式 ::= 变量 | 常量
qv表达式 ::= '?' | '(' q表达式 ':' v表达式 ')'
q表达式 ::= 变量 | 常量
v表达式 ::= 变量 | 常量

分组子句 ::= '\group by' 属性表达式
排序子句 ::= '\order by' 属性表达式 ['desc' | 'asc']
筛选子句 ::= '\filter' 比较表达式
比较表达式 ::= 属性表达式 比较运算符 常量 | '(' 函数表达式 比较运算符 常量 ')' | 属性表达式 匹配运算符 "'" 常量 "'"

函数名 ::= 'count' | 'avg' | 'sum' | 'max' | 'min'
函数表达式 ::= 函数名 '(' 属性表达式 ['of' 变量] ')'

属性表达式 ::= 变量 | 属性表达式 '.' 常量 //s.p1.p2.p3......
常量 ::= char | 常量 char
变量 ::= 变量I | 变量II | 变量III
变量I ::= '?'
变量II ::= 变量I ('x' | 'y' | 'z')
变量III ::= 变量II ('1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9')
比较运算符 ::= '=' | '!=' | '>' | '>=' | '<' | '<='
匹配运算符 ::= '\match'

计算语句 ::= 变量 '=' 函数表达式

答案语句 ::= 'ANS' 属性表达式 | 答案语句 ',' 属性表达式

//End
